import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id("java")
    id("io.freefair.lombok") version "6.0.0-m2"
    id("com.github.johnrengelman.shadow") version "7.0.0"
}

var name = "MoneyDrop"

description = "MoneyDrop plugin for Tonierbobcat966"
group = "net.j4c0b3y"
version = "2.0.3"

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/central" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    maven { url "https://nexus.phoenixdevt.fr/repository/maven-public/" }
    maven { url "https://maven.enginehub.org/repo/" }
}


dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly "io.lumine:Mythic-Dist:5.2.0"
    compileOnly "net.Indyuce:MMOCore-API:1.12.1-SNAPSHOT"
    compileOnly "io.lumine:MythicLib-dist:1.6.2-SNAPSHOT"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT"
    implementation "dev.dejvokep:boosted-yaml-spigot:1.4"
    implementation "net.kyori:adventure-api:4.14.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.0"
    implementation "net.kyori:adventure-text-minimessage:4.14.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.14.0"
}

compileJava {
    options.compilerArgs.add "-XDignore.symbol.file"
    options.compilerArgs.add "-Werror"
    options.fork = true
    options.forkOptions.executable = "javac"
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": version,
            "name": name,
            "description": description,
    ]
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

def outputTasks() {
    ["shadowJar"].stream().map({tasks.findByPath(it)})
}

tasks.register('copyJars', Copy) {
    outputTasks().forEach({from(it)})
    rename "(.*)-all.jar", (name + "-" + version + ".jar")
    into file("jars")
}

tasks.register('cleanJars') {
    delete file("jars")
}

clean.dependsOn "cleanJars"
build.dependsOn "shadowJar"
build.dependsOn "copyJars"